// --------------------
// plugins
// --------------------

plugins {
    id 'com.palantir.git-version'   version '0.8.0'
    id 'co.riiid.gradle'            version '0.4.2'
}

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven-publish'

// --------------------
// dependencies
// --------------------

repositories {
    jcenter()
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

dependencies {
    // COOL
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "net.praqma:praqmajutils:1.0.0"

    // Other
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'org.jfrog.artifactory.client:artifactory-java-client-services:1.2.2'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

// --------------------
// project configuration
// --------------------

project.group           = 'com.praqma'
project.version         = gitVersion()
project.description     = 'Migration engine used to migrate from an arbitrary VCS to git'


// --------------------
// application plugin
// --------------------

project.mainClassName   = 'toGit.Run'

startScripts {
    doLast {
        // The start scripts can't handle a project name starting with a number.
        // They'd contain use of a parameter called '$2GIT_OPTS', in which the '$2' was problematic.
        unixScript.text     = unixScript.text.replaceAll('2GIT_OPTS', 'TOGIT_OPTS')
        windowsScript.text  = windowsScript.text.replaceAll('2GIT_OPTS', 'TOGIT_OPTS')
    }
}

// --------------------
// github plugin
// --------------------

github {
    owner   = 'Praqma'
    repo    = '2git'
    token   = project.hasProperty("GITHUB_TOKEN") ? "$GITHUB_TOKEN" : "No token provided"
    tagName = gitVersion()
    name    = gitVersion()
    assets  = [
        "build/distributions/2git-${gitVersion()}.zip",
        "build/distributions/2git-${gitVersion()}.tar"
    ]
    targetCommitish = 'master'
}

githubRelease.group = "Publishing"
githubRelease.description = "Creates a release on GitHub"
